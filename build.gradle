plugins {
	id 'org.springframework.boot' version '2.2.5.RELEASE'
	id 'io.spring.dependency-management' version '1.0.9.RELEASE'
	id 'org.flywaydb.flyway' version '6.3.0'
	id 'java'
}

group = 'com.bookinggo'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-data-rest'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.retry:spring-retry:1.2.4.RELEASE'

	implementation 'mysql:mysql-connector-java:8.0.19'
	implementation 'org.modelmapper:modelmapper:2.3.6'

	implementation 'net.logstash.logback:logstash-logback-encoder:5.3'

	testImplementation 'com.h2database:h2:1.4.+'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	
	compileOnly 'org.projectlombok:lombok:1.18.12'
	annotationProcessor 'org.projectlombok:lombok:1.18.12'

	testCompileOnly 'org.projectlombok:lombok:1.18.12'
	testAnnotationProcessor 'org.projectlombok:lombok:1.18.12'
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
	testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.3.1'

	testImplementation 'com.wix:wix-embedded-mysql:4.6.+'
	testImplementation 'org.flywaydb:flyway-core:6.0.+'
	testImplementation 'com.h2database:h2:1.4.+'
	testImplementation 'org.mockito:mockito-junit-jupiter:2.23.+'
	testImplementation 'org.springframework.cloud:spring-cloud-contract-wiremock:2.2.4.RELEASE'
}

test {
	useJUnitPlatform()
	testLogging {
		events "passed", "skipped", "failed"
		afterSuite { desc, result ->
			if (!desc.parent) { // will match the outermost suite
				println "***** TEST Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped) *****"
			}
		}
	}
}

flyway {
	driver = 'com.mysql.cj.jdbc.Driver'
	url = 'jdbc:mysql://localhost:3360/restfuldemodb'
	user = "root"
	password = "pass"
	schemas = ['restfuldemodb']
	locations = ['filesystem:./src/main/resources/db/migration']
	outOfOrder = "true"
}

class ShowDate extends DefaultTask {
	String dateMessage = "Date is: "

	@TaskAction
	void showDate() {
		println dateMessage + new Date()
	}
}

task showDate(type: ShowDate)

task customShowDate(type: ShowDate) {
	dateMessage = "Custom date is: "
}
